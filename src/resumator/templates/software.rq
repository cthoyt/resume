PREFIX target: <http://www.wikidata.org/entity/@@@ qid @@@>

SELECT DISTINCT
    ?item ?itemLabel ?itemDescription
    (GROUP_CONCAT(DISTINCT ?license; separator=", ") AS ?licenses)
    (SAMPLE(?repo_) AS ?repo)
    (SAMPLE(?website_) AS ?website)
WHERE
{
    VALUES ?type { wd:Q7397 wd:Q29642950 }
    ?item wdt:P170|wdt:P178|wdt:P767 target: .
    ?item wdt:P31/wdt:P279* ?type .
    OPTIONAL { ?item wdt:P275/wdt:P1813 ?license } .
    OPTIONAL { ?item wdt:P1324 ?repo_ } .
    OPTIONAL { ?item wdt:P856 ?website_ } .
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?item ?itemLabel ?itemDescription
